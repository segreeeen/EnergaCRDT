plugins {
    id 'java'
    id 'maven-publish'
}

group = 'at.felixb'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':energa-core')

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.mockito:mockito-core:5.12.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.12.0"

}

test {
    useJUnitPlatform()
}

sourceSets {
    performanceTest {
        java.srcDir file("src/performanceTest/java")
        resources.srcDir file("src/performanceTest/resources")
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'energa-presence'
        }
    }

    repositories {
        maven {
            name = "nexus"
            url = uri("https://nx.felixb.at/repository/libs/")

            credentials {
                username = System.getenv("NEXUS_USERNAME")
                password = System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}

tasks.register('performanceTest', Test) {
    description = "Runs performance benchmarks"
    group = "verification"

    testClassesDirs = sourceSets.performanceTest.output.classesDirs
    classpath = sourceSets.performanceTest.runtimeClasspath

    useJUnitPlatform()
}
